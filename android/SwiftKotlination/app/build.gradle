apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.0'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId 'fr.jhandguy.swiftkotlination'
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName '1.0'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
        }
    }
    testOptions {
        execution 'ANDROID_TEST_ORCHESTRATOR'
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
        }
    }
    sourceSets {
        main {
            java.srcDirs = ['src/main/kotlin']
            manifest.srcFile 'src/main/AndroidManifest.xml'
        }

        test {
            java.srcDirs = ['src/test/kotlin']
        }

        androidTest {
            java.srcDirs = ['src/androidTest/kotlin']
            manifest.srcFile 'src/androidTest/AndroidManifest.xml'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation              'androidx.appcompat:appcompat:1.0.2'
    implementation              'androidx.recyclerview:recyclerview:1.0.0'
    implementation              'androidx.constraintlayout:constraintlayout:1.1.3'
    androidTestImplementation   'androidx.test:core:1.0.0'
    androidTestImplementation   'androidx.test:runner:1.1.0'
    androidTestImplementation   'androidx.test:rules:1.1.0'
    androidTestImplementation   'androidx.test.ext:junit:1.0.0'
    androidTestImplementation   'androidx.test.espresso:espresso-core:3.1.0'
    androidTestImplementation   'androidx.test.espresso:espresso-intents:3.1.0'
    androidTestUtil             'androidx.test:orchestrator:1.1.0'

    implementation              'org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.9.0'
    implementation              'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.1'
    implementation              'org.jetbrains.anko:anko:0.10.7'
    implementation              'org.jetbrains.anko:anko-recyclerview-v7:0.10.7'
    implementation              'org.jetbrains.anko:anko-constraint-layout:0.10.7'
    testImplementation          'org.jetbrains.kotlin:kotlin-test-junit:1.3.10'
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest'/*, 'createDebugCoverageReport'*/]) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = [ '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*', '**/*$*.*']
    def debugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/kotlin"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec'//, 'outputs/code-coverage/connected/*coverage.ec'
    ])
}
