apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'jacoco'

jacoco {
    setToolVersion(jacocoVersion)
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId 'fr.jhandguy.swiftkotlination'
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName '1.0'
        testInstrumentationRunner 'fr.jhandguy.swiftkotlination.runner.AndroidTestRunner'
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
        }
    }
    testOptions {
        //TODO: execution 'ANDROID_TEST_ORCHESTRATOR'
        animationsDisabled true
        unitTests {
            includeAndroidResources = true
        }
    }
    sourceSets {
        main {
            java.srcDirs = ['src/main/kotlin']
            manifest.srcFile 'src/main/AndroidManifest.xml'
        }
        test {
            java.srcDirs = ['src/test/kotlin', 'src/sharedTest/kotlin']
            resources.srcDirs = ['src/test/resources', 'src/sharedTest/resources']
        }
        androidTest {
            java.srcDirs = ['src/androidTest/kotlin', 'src/sharedTest/kotlin']
            resources.srcDirs = ['src/androidTest/resources', 'src/sharedTest/resources']
        }
        debug {
            java.srcDirs = ['src/main/kotlin']
            manifest.srcFile 'src/debug/AndroidManifest.xml'
        }
    }
}

dependencies {
    implementation              "androidx.appcompat:appcompat:$appCompatVersion"
    implementation              "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    implementation              "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    testImplementation          "androidx.test:core:$testVersion"
    androidTestImplementation   "androidx.test:core:$testVersion"
    androidTestImplementation   "androidx.test:runner:$testVersion"
    androidTestImplementation   "androidx.test:rules:$testVersion"
    androidTestImplementation   "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation   "androidx.test.espresso:espresso-intents:$espressoVersion"

    implementation              "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serializationVersion"
    implementation              "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    implementation              "org.jetbrains.anko:anko:$ankoVersion"
    implementation              "org.jetbrains.anko:anko-recyclerview-v7:$ankoVersion"
    implementation              "org.jetbrains.anko:anko-constraint-layout:$ankoVersion"
    testImplementation          "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"

    testImplementation          "junit:junit:$junitVersion"
    testImplementation          "org.robolectric:robolectric:$robolectricVersion"

    androidTestImplementation   "tools.fastlane:screengrab:$screengrabVersion"
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*', '**/*$*.*']
    def debugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/kotlin"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
    ])
}
