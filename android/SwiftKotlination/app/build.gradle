apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.0'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId 'fr.jhandguy.swiftkotlination'
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName '1.0'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
        }
    }
    testOptions {
//        execution 'ANDROID_TEST_ORCHESTRATOR'
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
        }
    }
    sourceSets {
        main {
            java.srcDirs = ['src/main/kotlin']
            manifest.srcFile 'src/main/AndroidManifest.xml'
        }

        test {
            java.srcDirs = ['src/test/kotlin']
        }

        androidTest {
            java.srcDirs = ['src/androidTest/kotlin']
            manifest.srcFile 'src/androidTest/AndroidManifest.xml'
        }
    }
}

dependencies {
    implementation              'androidx.appcompat:appcompat:1.0.2'
    implementation              'androidx.recyclerview:recyclerview:1.0.0'
    implementation              'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation              'org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.10.0'
    implementation              'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1'
    implementation              'org.jetbrains.anko:anko:0.10.8'
    implementation              'org.jetbrains.anko:anko-recyclerview-v7:0.10.8'
    implementation              'org.jetbrains.anko:anko-constraint-layout:0.10.8'
    testImplementation          'org.jetbrains.kotlin:kotlin-test-junit:1.3.20'
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest'/*, 'createDebugCoverageReport'*/]) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = [ '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*', '**/*$*.*']
    def debugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/kotlin"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec'//, 'outputs/code-coverage/connected/*coverage.ec'
    ])
}
